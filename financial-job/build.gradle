buildscript {
	ext {
		springBootVersion = '2.1.0.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.franzbecker:gradle-lombok:1.14")
		classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.21.0')
	}
}

def explodedJarDir = "exploded"

apply plugin: 'io.franzbecker.gradle-lombok'
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

group = 'com.nixsolutions'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	maven { url 'https://jitpack.io' }
	mavenCentral()
	mavenLocal()
}

lombok {
	version = '1.18.4'
	sha256 = ""
}

task unpack(type: Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/${explodedJarDir}")
}

docker {
	name 'gcr.io/innate-life-217919/financial-job:'+version
	copySpec.from(tasks.unpack.outputs).into("${explodedJarDir}")
	buildArgs(['EXPLODED_JAR_DIR': "${explodedJarDir}"])
}

dependencies {
	api('com.nixsolutions:financial-domain-starter:0.0.1-SNAPSHOT')

	implementation('org.springframework.boot:spring-boot-starter-batch')
	implementation group: 'commons-collections', name: 'commons-collections', version: '3.2.1'
	implementation group: 'commons-io', name: 'commons-io', version: '2.6'

	runtime group: 'com.h2database', name: 'h2', version: '1.4.197'
	runtimeOnly('org.springframework.boot:spring-boot-devtools')
	compileOnly('org.projectlombok:lombok')
	compileOnly('org.springframework.boot:spring-boot-configuration-processor')

	testImplementation group: 'com.squareup.okhttp3', name: 'mockwebserver', version: '3.2.0'
	testImplementation('org.springframework.boot:spring-boot-starter-test')
	testImplementation('com.github.sbrannen:spring-test-junit5:1.3.1')
}
