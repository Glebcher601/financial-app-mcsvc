buildscript {
    ext {
        kotlinVersion = '1.2.70'
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-noarg:${kotlinVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'kotlin-jpa'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

group = 'com.nixsolutions'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
def explodedJarDir = "exploded"

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("build/${explodedJarDir}")
}

docker {
    name 'gcr.io/innate-life-217919/authorization-server:'+version
    copySpec.from(tasks.unpack.outputs).into("${explodedJarDir}")
    buildArgs(['EXPLODED_JAR_DIR': "${explodedJarDir}"])
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {
    implementation('com.nixsolutions:financial-domain-starter:0.0.1-SNAPSHOT')

    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    implementation("org.jetbrains.kotlin:kotlin-reflect")

    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.1.0'
    testImplementation 'org.junit.platform:junit-platform-runner:1.1.0'
    testImplementation('com.fasterxml.jackson.core:jackson-databind:2.9.0')
    testImplementation 'org.junit.vintage:junit-vintage-engine:5.1.0'
    testImplementation group: 'io.projectreactor', name: 'reactor-test'
}
